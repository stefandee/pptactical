// Support Bot - by Karg
// scan for friendly units under fire and attack
// their attackers

const STATE_DEFEND = 1;
const STATE_SEARCH = 2;
const STATE_ATTACK = 4;

var mov, scan, attack, uStatus;
var enemyId = -1, enemySet = 0, state = STATE_SEARCH, friendId = -1;

ScanFriendly()
{
  var uNo, i, uid, uRel;

  uNo = Scan();

  if (uNo >= 0)
  {
    for(i = 0; i < uNo; i++)
    {
      uid = UnitInView(i);
       
      // check if unit is friendly and count its attackers
      uRel = UnitRelation(uid);
      if (uRel == 1 || uRel == 2)
      {
	// check if unit uid is dead
	uStatus = UnitStatus(uid); 
	if (uStatus == 1)
	{
	  continue;
	}

	// check if this unit is attacked by some one
	if (AttackerOfCount(uid) == 0)
	{
	  continue;
	}
	
	// pick one of my friend attackers - may be a random one
	enemyId  = AttackerOf(uid, 0);
        friendId = uid;
        if (enemyId == -1)
	{
	  continue;
	}

        // check visual range and move accordingly
	LookTo(friendId);
	if (FireTo(enemyId) == 1)
	{
          enemySet = 1;
	  state    = STATE_DEFEND;
	  LookTo(enemyId);
	  PostMessage("Attacking your attacker!");
	}

	return;
      }
    }
  }
}

ScanEnemy()
{
  var eNo, i, uid;

  if (enemySet == 0)
  {
    eNo = Scan();

    if (eNo >= 0)
    {
      for(i = 0; i < eNo; i++)
      {
        uid = UnitInView(i);
        
        // check if unit is an enemy and is in weapon range
        if (UnitRelation(uid) == 3 && UnitDist(uid) < WeaponRange(1))
	{
	  // check if unit uid is dead
	  uStatus = UnitStatus(uid); 
	  if (uStatus == 1)
	  {
	    continue;
	  }

          enemyId = uid; 
          LookTo(enemyId);

	  if (FireTo(enemyId) != 1)
	  {
             enemyId = -1;
	     continue;
          }

          PostMessage("Goin' in for a kill!");
	  state = STATE_ATTACK;
	  return;
        }
      }
    }
  }
}

main()
{
  if (state == STATE_SEARCH)
  {
    ScanFriendly();
  }

  if (state == STATE_SEARCH)
  {
    ScanEnemy();
  }

  if (state == STATE_SEARCH)
  {
    if (Moving() == 0)
    {
      Move(MyX() + random(10) - 5, MyY() + random(10) - 5);
    }

    if (Moving() == 1 && random(10) > 7)
    {
      SetScanAngle(GetScanAngle() + random(30) - 15);
    }
  }

  // return to the default state
  if (state == STATE_DEFEND || state == STATE_ATTACK)
  {
    if (UnitDist(enemyId) > WeaponRange(1))
    {
      state = STATE_SEARCH;
    } 

    uStatus = UnitStatus(enemyId); 

    if (uStatus == 0 || uStatus == 1 || uStatus == -1)
    {
      state = STATE_SEARCH;
    }
  }

  // my protegee is dead? 
  if (state == STATE_DEFEND)
  {
    uStatus = UnitStatus(friendId); 

    if (uStatus == 0 || uStatus == 1 || uStatus == -1)
    {
      state = STATE_SEARCH;
    }
  }
}